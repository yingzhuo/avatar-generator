plugins {
	id 'maven-publish'
	id 'signing'
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java

			groupId = 'com.github.yingzhuo'
			artifactId = 'avatar-generator'
			version = "${thisProjectVersion}"

			versionMapping {
				usage('java-api') {
					fromResolutionOf('runtimeClasspath')
				}
				usage('java-runtime') {
					fromResolutionResult()
				}
			}

			pom {
				name = 'avatar-generator'
				description = 'Avatar Generator'
				url = 'https://github.com/yingzhuo/avatar-generator'
				inceptionYear = '2020'

				licenses {
					license {
						name = 'Apache License, Version 2.0'
						url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}

				developers {
					developer {
						id = 'yingzhuo'
						name = '应卓'
						email = 'yingzhor@gmail.com'
						roles = ['author']
						url = 'https://github.com/yingzhuo'
					}
				}

				scm {
					url = 'git@github.com:yingzhuo/avatar-generator.git'
					connection = 'scm:git:git@github.com:yingzhuo/avatar-generator.git'
					developerConnection = 'scm:git:git@github.com:yingzhuo/avatar-generator.git'
				}
			}
		}
	}

	repositories {
		maven {
			name = "Local"
			url = layout.buildDirectory.dir('repos/bundles')
		}
	}
}

signing {
	useGpgCmd()
	sign publishing.publications.mavenJava
}

tasks.withType(GenerateModuleMetadata).configureEach {
	enabled = false
}
